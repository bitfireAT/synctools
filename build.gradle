buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.+'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

apply plugin: 'com.android.library'


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'AllowBackup'
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.5'
    provided 'org.projectlombok:lombok:1.16.14'

    // ez-vcard to parse/generate VCards
    compile('com.googlecode.ez-vcard:ez-vcard:0.10.2') {
        // hCard functionality not needed
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
        // jCard functionality not needed
        exclude group: 'com.fasterxml.jackson.core'
    }

    androidTestCompile 'com.android.support.test:runner:+'
    androidTestCompile 'com.android.support.test:rules:+'
    androidTestCompile 'junit:junit:4.12'

    testCompile 'junit:junit:4.12'
}

// grant permissions for unit tests
task grantPermissions(dependsOn: 'installDebugAndroidTest') {
    doLast {
        def adb = android.getAdbExe().toString()
        [
                'android.permission.READ_CONTACTS',
                'android.permission.WRITE_CONTACTS'
        ].each {
            "${adb} shell pm grant at.bitfire.vcard4android.test ${it}".execute()
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connectedDebugAndroidTest')) {
        task.dependsOn grantPermissions
    }
}
