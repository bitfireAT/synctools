
buildscript {
    ext.versions = [
        kotlin: '1.5.31',
        dokka: '1.5.0',
        // latest Apache Commons versions that don't require Java 8 (Android 7)
        commonsIO: '2.6',
        commonsText: '1.3'
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

android {
    compileSdkVersion 31
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 16        // Android 4.1
        targetSdkVersion 31     // Android 12
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'AllowBackup'
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    // TODO currently doesn't work with dokka 1.5.0, but dokka 0.10.1 doesn't work with new gradle
    /*dokka.configuration {
        sourceLink {
            remoteUrl.set(java.net.URL("https://gitlab.com/bitfireAT/vcard4android/tree/master/"))
            remoteLineSuffix.set("#L")
        }
        jdkVersion = 7
    }*/
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    implementation 'androidx.annotation:annotation:1.2.0'
    // noinspection GradleDependency
    implementation "commons-io:commons-io:${versions.commonsIO}"
    // noinspection GradleDependency
    implementation "org.apache.commons:commons-text:${versions.commonsText}"

    // ez-vcard to parse/generate vCards
    api('com.googlecode.ez-vcard:ez-vcard:0.11.3') {
        // hCard functionality not needed
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
    }

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    testImplementation 'junit:junit:4.13.2'
}
