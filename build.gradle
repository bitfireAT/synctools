
buildscript {
    ext.kotlin_version = '1.1.61'
    ext.dokka_version = '0.9.15'

    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
    }
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'AllowBackup'
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    compile 'org.apache.commons:commons-text:1.1'
    compile 'commons-io:commons-io:2.6'

    // ez-vcard to parse/generate VCards
    compile('com.googlecode.ez-vcard:ez-vcard:0.10.3') {
        // hCard functionality not needed
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
        // jCard functionality not needed
        exclude group: 'com.fasterxml.jackson.core'
    }

    //noinspection GradleDynamicVersion
    androidTestCompile 'com.android.support.test:runner:+'
    //noinspection GradleDynamicVersion
    androidTestCompile 'com.android.support.test:rules:+'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.projectlombok:lombok:1.16.16'

    testCompile 'junit:junit:4.12'
    testCompile 'org.projectlombok:lombok:1.16.16'
}

// grant permissions for unit tests
task grantPermissions(dependsOn: 'installDebugAndroidTest') {
    doLast {
        def adb = android.getAdbExe().toString()
        [
                'android.permission.READ_CONTACTS',
                'android.permission.WRITE_CONTACTS'
        ].each {
            "${adb} shell pm grant at.bitfire.vcard4android.test ${it}".execute()
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('connectedDebugAndroidTest')) {
        task.dependsOn grantPermissions
    }
}
